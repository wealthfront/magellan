apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

group = GROUP
version = VERSION_NAME

android {
  compileSdkVersion Versions.compileSdkVersion

  resourcePrefix 'magellan_'

  defaultConfig {
    minSdkVersion Versions.minSdkVersion
    targetSdkVersion Versions.targetSdkVersion
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  compileOptions {
    setSourceCompatibility(JavaVersion.VERSION_1_8)
    setTargetCompatibility(JavaVersion.VERSION_1_8)
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  buildFeatures {
    viewBinding = true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  if (!name.contains("UnitTest")) {
    kotlinOptions.freeCompilerArgs = ['-Xjvm-default=compatibility', '-Xexplicit-api=strict', '-Xopt-in=kotlin.RequiresOptIn']
  } else {
    kotlinOptions.freeCompilerArgs = ['-Xopt-in=kotlin.RequiresOptIn']
  }
  kotlinOptions.allWarningsAsErrors = true
  kotlinOptions.jvmTarget = "1.8"
}

dependencies {
  implementation Dependencies.appCompat
  implementation Dependencies.lifecycle
  implementation Dependencies.inject
  implementation Dependencies.coroutines
  implementation Dependencies.coroutinesAndroid
  api Dependencies.blend

  testImplementation project(':internal-test-support')
  testImplementation Dependencies.testCore
  testImplementation Dependencies.testCoreKtx
  testImplementation Dependencies.junit
  testImplementation Dependencies.extJunitKtx
  testImplementation Dependencies.truth
  testImplementation Dependencies.mockito
  testImplementation Dependencies.archTesting
  testImplementation Dependencies.robolectric
  testImplementation Dependencies.coroutinesTest
  testImplementation Dependencies.blendTest

// Bug in AGP: Follow this issue - https://issuetracker.google.com/issues/141840950
// lintPublish project(':magellan-lint')
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
